PRO WVT_INTERPOL,INFILE,CNTSFILE,EXPFILE,OUTFILE,SNR=SNR,$
     CENTER=CENTER,MAXCELLSIZE=MAXCELLSIZE,SUBIMGSIZE=SUBIMGSIZE
; Applies the Weighted Voronoi Tesselations (Diehl & Statler, 2006,
; MNRAS 368, 497) to the fits image called INFILE, determines where
; all the centers of the resulting tesselation cells are located and
; the corresponding pixel values, then stretches a surface across
; those points using min_curve_surf.  We could just leave the image
; after applying WVT, but the problem is that each cell, which can be
; several pixels, is all the same value and has a honeycomb shape,
; which seriously impacts the ability to draw realistic contours using
; the resulting image.  So effectively, we are removing those
; artifical plataeus and forcing there to be some gradients in the
; pixels inbetween the centers of these cells, allowing for a more
; realistic profile.
BLANK=-9999                   ; value of pixels that need to be filled in with interpolated numbers
FXREAD,CNTSFILE,CNTS          ; counts per pixel
FXREAD,EXPFILE,EFFEXP         ; effective area and effective exposure time map (cm^2 s photon/count)

NOISE=SQRT(CNTS)/EFFEXP
; The noise is computed by sqrt(counts)/expmap. Note that the flux
; image, in units of photons/s/cm^2/pixel is generated by dividing the
; counts image (soft_thresh.img, output from ciao command "fluximage")
; by the effective exposure map (soft_thres.expmap), which also
; accounts for the effective area if fluximage is run with the default
; for the type of exposure map that is generated.  See Diehl &
; Statler, 2006, MNRAS 368, 497 eqn 9.

FXREAD,INFILE,PHOTFLUX,HDR  ; photons/s/cm^2/pixel

; target Signal-to-noise ratio per tesselation cell, can be changed to whatever
IF NOT KEYWORD_SET(SNR) THEN SNR=4.
; A target SNR of 4-10 is standard for x-ray, see pg 8 of WVT manual
; a temperature map would require SNR~30-100, and integral field
; spectroscopy, SNR~50 per bin would be reasonable to extract stellar
; kinematics info from galaxy spectra.

IF NOT KEYWORD_SET(MAXCELLSIZE) THEN MAXCELLSIZE=5.
; maximum size of a cell, on a side (to be used to compute max square
; area)

IF NOT KEYWORD_SET(SUBIMGSIZE) THEN SUBIMGSIZE=50
; size of subimage region that is the viable region
; (e.g., the part without margins that will be thrown out)

RESULT=NN_INTERPOL(PHOTFLUX,NOISE,SNR,CTSIMAGE=CNTS,$
                   CENTER=CENTER,MAXCELLSIZE=MAXCELLSIZE)


WRITEFITS,OUTFILE+'_wvt.fits',RESULT,HDR


END
